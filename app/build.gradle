plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlinx.kover'
    id 'com.google.devtools.ksp' version '1.7.20-1.0.8'
}

android {
    namespace 'c.m.storyapp'
    compileSdk 33

    defaultConfig {
        applicationId "c.m.storyapp"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        multiDexEnabled true

        // load configuration
        Properties properties = new Properties()
        InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
        properties.load(inputStream)
        def baseURL = properties.getProperty('baseURL')

        // Base URL for API
        buildConfigField "String", "BASE_URL_API", baseURL
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.2"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }

}

dependencies {
    // Compose BOM
    def composeBom = platform('androidx.compose:compose-bom:2022.10.00')
    implementation composeBom

    // Material Design 3
    implementation 'androidx.compose.material3:material3'

    // the icons but not the material library (e.g. when using Material3 or a
    // custom design system based on Foundation)
    implementation 'androidx.compose.material:material-icons-core'
    // Add full set of material icons
    implementation 'androidx.compose.material:material-icons-extended'
    // Add window size utils
    implementation 'androidx.compose.material3:material3-window-size-class'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // accompanist Swipe to refresh
    implementation "com.google.accompanist:accompanist-swiperefresh:0.26.1-alpha"

    // Integration with activities
    implementation 'androidx.activity:activity-compose'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'

    // Compose Destination
    implementation "io.github.raamcosta.compose-destinations:core:1.6.15-beta"
    ksp "io.github.raamcosta.compose-destinations:ksp:1.6.15-beta"

    // Kotlin dependencies
    //noinspection GradleDependency
    implementation 'androidx.core:core-ktx:1.7.0'

    // internal logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:2.43"
    kapt "com.google.dagger:hilt-android-compiler:2.43"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"
    implementation "androidx.paging:paging-compose"

    // Room
    implementation "androidx.room:room-runtime:2.4.3"
    annotationProcessor("androidx.room:room-compiler:2.4.3")
    kapt "androidx.room:room-compiler:2.4.3"

    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:2.4.3"

    // Google Maps
    implementation 'com.google.android.gms:play-services-maps:18.1.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Okhttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.10.0"))
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"

    // CameraX
    implementation "androidx.camera:camera-camera2:1.3.0-alpha01"
    implementation "androidx.camera:camera-lifecycle:1.3.0-alpha01"

    // compose image loading
    implementation "com.github.skydoves:landscapist-coil:1.4.4"

    // Preferences DataStore (SharedPreferences like APIs)
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Splash screen Api
    implementation("androidx.core:core-splashscreen:1.0.0")

    // Work Manager (now, just for handle splash screen problem in android 12)
    implementation 'androidx.work:work-runtime:2.8.0-beta02'

    // test dependencies
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.4.0'
    testImplementation 'org.mockito:mockito-inline:4.4.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
    testImplementation "androidx.room:room-testing:2.4.3"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    androidTestImplementation composeBom
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

kover {
    instrumentation {
        excludeTasks.add "testReleaseUnitTest" // exclude testReleaseUnitTest from instrumentation
    }
}